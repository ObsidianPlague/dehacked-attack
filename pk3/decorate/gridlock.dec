ACTOR GridlockCardinal : Cyberdemon
{
  Health 4800 //Change: Health lowered from 8000 to 4800
  Speed 16
  PainChance 32
  +FLOAT
  +NOGRAVITY
  -NORADIUSDMG
  Obituary "%o was splattered by a Gridlock Cardinal."
  PainSound "brain/pain"
  Tag "Gridlock Cardinal"
  States
  {
  Spawn:
    TNT1 A 0
    TNT1 A 0 A_JumpIf(ACS_NamedExecuteWithResult("Dehacked_MonsterReset", DEH_GRIDLOCK, 30), "Reset")
	GCRD A 0 A_JumpIf(ACS_NamedExecuteWithResult("Dehacked_BossTargetCount") > 0, "Phase2")
  Idle:
    GCRD AB 3 A_Look
    Loop
  Phase2:
	TNT1 A 0 A_ChangeFlag("COUNTKILL", 0)
	GCRD A 0 A_JumpIf(ACS_NamedExecuteWithResult("Dehacked_BossCheck") == 2, "Reset")
	GCRD A 0 A_SpawnItemEx("GridlockCardinalPhase2", 0, 0, 0, 0, 0, 0, 0, SXF_TRANSFERAMBUSHFLAG|SXF_TRANSFERPOINTERS|SXF_NOCHECKPOSITION, 0)
	Stop
  Reset:
	GCRD A 0 A_SpawnItemEx("Cyberdemon", 0, 0, 0, 0, 0, 0, 0, SXF_TRANSFERAMBUSHFLAG|SXF_TRANSFERPOINTERS|SXF_NOCHECKPOSITION, 0)
	Stop
  See:
    GCRD AB 3 A_Chase
    Loop
  Missile:
    GCRD C 6 A_FaceTarget
    GCRD D 0 A_FatAttack1
    GCRD D 0 A_FatAttack2
    GCRD D 6 Bright A_FatAttack3
    GCRD C 6 A_FaceTarget
    GCRD D 6 Bright A_SkelMissile
    GCRD C 10 A_FaceTarget
	Goto MissileLoop
  MissileLoop:
    GCRD D 2 Bright A_BSpiAttack
	GCRD C 2 A_SpidRefire
    Loop
  Pain:
    GCRD E 10 A_Pain
    GCRD C 6 A_FaceTarget
    GCRD D 6 Bright A_CyberAttack
    Goto See
  Death:
    GCRD F 10
    GCRD G 10 A_Scream
    GCRD HIJ 10
    GCRD K 10 A_NoBlocking
    GCRD L 10
    GCRD M 30
    GCRD M -1 A_BossDeath
    Stop
  }
}

ACTOR GridlockCardinalPhase2 : GridlockCardinal
{
  Tag "Gridlock Cardinal Mark II"
  States
  {
  Spawn:
    GCRD AB 3 A_Look
    Loop
  Idle:
    GCRD AB 3 A_Look
    Loop
  Pain:
    GCRD E 10 A_Pain
    GCRD C 6 A_FaceTarget
    GCRD D 0 A_BrainSpit
    GCRD D 6 Bright A_BrainSpit
    Goto See
  }
}

ACTOR GridlockCardinalFinalPhase1 : GridlockCardinal
{
  Health 8000
  Tag "Gridlock Cardinal (Phase I)"
  Speed 24
  States
  {
  Spawn:
    GCRD AB 3 A_Look
    Loop
  Idle:
    GCRD AB 3 A_Look
    Loop
  Death:
    TFOG A 6 Bright A_NoBlocking
	TFOG B 6 Bright A_BrainSpit("DEHCardinalVileSpawnShot")
	TFOG A 6 Bright A_BrainSpit("DEHCardinalMinibossSpawnShot")
	TFOG B 6 Bright A_BrainSpit("DEHCardinalSpiderSpawnShot")
	TFOG C 6 Bright A_BrainSpit("DEHCardinalVileSpawnShot")
	TFOG D 6 Bright A_BrainSpit("DEHCardinalMinibossSpawnShot")
	TFOG E 6 Bright A_BrainSpit("DEHCardinalMastermindSpawnShot")
    TFOG FGHIJ 6 Bright
	TNT1 A 1050
	TNT1 A 0 Bright A_BrainSpit("DEHCardinalVileSpawnShot")
	TNT1 A 0 Bright A_BrainSpit("DEHCardinalMinibossSpawnShot")
	TNT1 A 0 A_SpawnItemEx("GridlockCardinalFinalPhase2",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION|SXF_TELEFRAG|SXF_TRANSFERPOINTERS,0)
	TNT1 A 0 A_SpawnItemEx("TeleportFog",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION,0)
    Stop
  }
}

ACTOR GridlockCardinalFinalPhase2 : GridlockCardinal
{
  Tag "Gridlock Cardinal (Phase II)"
  Health 6000
  +MISSILEEVENMORE
  Speed 24
  States
  {
  Spawn:
    GCRD AB 3 A_Look
    Loop
  Idle:
    GCRD AB 3 A_Look
    Loop
  Teleport: //Change: Added this as an attack since this was map geometry in the original
	GCRD A 0 A_SpawnItemEx("TeleportFog",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION,0)
    GCRD AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 0 A_Wander
	GCRD A 0 A_SpawnItemEx("TeleportFog",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION,0)
    Goto See
  Missile:
	GRCD A 0 A_Jump(85, "Teleport")
    GCRD C 6 A_FaceTarget
    GCRD D 0 A_FatAttack1
    GCRD D 0 A_FatAttack2
    GCRD D 6 Bright A_FatAttack3
    GCRD C 6 A_FaceTarget
    GCRD D 6 Bright A_SkelMissile
    GCRD C 10 A_FaceTarget
	Goto MissileLoop
  MissileLoop:
    GCRD D 2 Bright A_BSpiAttack
	GCRD C 2 A_SpidRefire
    Loop
  Pain:
    GCRD E 10 A_Pain
    GCRD C 6 A_FaceTarget
    GCRD D 0 A_BrainSpit
    GCRD D 6 Bright A_BrainSpit
    Goto See
  Death:
    GCRD F 10 A_PlaySound("brain/death", 0, 1.0, 0, ATTN_NONE)
    GCRD G 10 A_Scream
    GCRD HIJ 10
    GCRD K 10 A_NoBlocking
    GCRD L 10
    GCRD M 50
    GCRD M -1 A_BrainDie
    Stop
  }
}
