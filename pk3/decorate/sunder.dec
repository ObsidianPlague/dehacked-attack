ACTOR HiveMother
{
  Health 2000
  Radius 60
  Height 110
  Mass 5000
  Speed 18
  PainChance 10
  Monster
  +FLOORCLIP
  +DONTMORPH
  +BOSS //CHANGE: Added to be more in line with the other minibosses.
  SeeSound "hivemother/sight"
  PainSound "hivemother/pain"
  DeathSound "hivemother/death"
  ActiveSound "hivemother/active"
  Obituary "%o was shredded by a hive mother."
  States
  {
  Spawn:
    TNT1 A 0
    TNT1 A 0 A_JumpIf(ACS_NamedExecuteWithResult("Dehacked_MonsterReset", DEH_SUNDER, 18), "Reset")
    Goto CheckSpiderCount
  Reset:
    TNT1 A 0 A_ChangeFlag("COUNTKILL", 0)
    TNT1 A 0 A_SpawnItemEx("DEHSpiderMastermind", 0, 0, 0, 0, 0, 0, 0, SXF_TRANSFERAMBUSHFLAG|SXF_TRANSFERPOINTERS|SXF_NOCHECKPOSITION, 0)
    Stop
  CheckSpiderCount:
	TNT1 A 1
	TNT1 A 1 A_JumpIf(ACS_NamedExecuteWithResult("Dehacked_SpiderCount"), "Reset")
	Goto Idle
  Idle:
    HVMM AB 10 A_Look
    Loop
  See:
    HVMM ABCDEF 3 A_Chase
    Loop
  Missile:
    HVMM GH 4 A_FaceTarget //CHANGE: The A_FatAttack order goes 3-2-1-3-2-1-3 to make it symmetrical,
    HVMM H 1 Bright A_FatAttack3 //as the original 1-2-3-1-2-3-1 was biased to the right.
    HVMM I 1 Bright A_FatAttack2
    HVMM H 1 Bright A_FatAttack1
    HVMM I 1 Bright A_FatAttack3
    HVMM H 1 Bright A_FatAttack2
    HVMM I 1 Bright A_FatAttack1
    HVMM H 3 Bright A_FatAttack3
	HVMM KLM 4 A_FaceTarget
    Goto See
  Pain:
	HVMM Y 3 //Change: This pain state is actually used
	HVMM Y 3 A_Pain
	HVMM Z 6
    HVMM NOPQ 3 A_FaceTarget
    HVMM R 3 Bright A_CyberAttack
    HVMM S 3 Bright A_FaceTarget //Change: Each of these A_FaceTarget calls are 3 tics instead of 1.
    HVMM R 3 Bright A_CyberAttack
    HVMM S 3 Bright A_FaceTarget
    HVMM R 3 Bright A_CyberAttack
    HVMM S 3 Bright A_FaceTarget
    HVMM TUVWX 3 A_FaceTarget
    HVMM AABBCCDDEEFF 1 A_Chase
    Goto See
  Death:
    HVM2 A 10
    HVM2 B 10 A_Scream
    HVM2 C 10
    HVM2 D 10 A_NoBlocking
    HVM2 EF 10
    HVM2 GHIJ 5
    HVM2 K -1
    Stop
  }
}

ACTOR HiveMotherSummon : HiveMother
{
  States
  {
  Spawn:
    TNT1 A 0
    TNT1 A 0 A_JumpIf(ACS_NamedExecuteWithResult("Dehacked_MonsterReset", DEH_SUNDER, 18), "Reset")
    Goto Idle
  Reset:
    TNT1 A 0 A_ChangeFlag("COUNTKILL", 0)
    TNT1 A 0 A_SpawnItemEx("HaymakerSummon", 0, 0, 0, 0, 0, 0, 0, SXF_TRANSFERAMBUSHFLAG|SXF_TRANSFERPOINTERS|SXF_NOCHECKPOSITION, 0)
    Stop
  Idle:
    HVMM AB 10 A_Look
    Loop
  }
}