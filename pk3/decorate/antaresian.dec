Actor AntaresianPlasma : ArachnotronPlasma
{
  Damage 4
  Speed 28
  SeeSound "antaresia/plasma"
  States
  {
  Spawn:
    RPLS A 5 Bright
    Loop
  Death:
    RPBX ABC 5 Bright
    Stop
  }
}

Actor FormerRanger
{
	Health 25
	Speed 8
	Radius 20
	Height 56
	ReactionTime 8
	PainChance 200
	Mass 100
	Monster
	+FLOORCLIP
	SeeSound "grunt/sight"
	PainSound "grunt/pain"
	DeathSound "grunt/death"
	ActiveSound "grunt/active"
	Obituary "%o was killed by a former ranger."
	Tag "Former Ranger"
	Dropitem "Cell" //Changed for consistency
	States
	{
	Spawn:
		APOS AABB 10 A_Look
		goto Spawn
	See:
		APOS AABBCCDD 3 A_Chase
		goto See
	Pain:
		APOS G 3 
		APOS G 3 A_Pain
		goto See
	Missile:
		APOS E 10 A_FaceTarget
		APOS F 8 Bright A_CustomMissile("AntaresianPlasma",32,0,0)
		APOS E 8
		goto See
	Death:
		APOS H 5 
		APOS I 5 A_Scream
		APOS J 5 A_Fall
		APOS K 5 
		APOS L -1 
		stop
	XDeath:
		APOS M 5 
		APOS N 5 A_XScream
		APOS O 5 A_Fall
		APOS PQRST 5 
		APOS U -1
		stop
	Raise:
		APOS KJIH 5 
		goto See
	}
}

Actor PlasmaElite : FormerCaptain
{
	Health 65
	PainChance 100
	Obituary "%o was destroyed by a plasma elite."
	Tag "Plasma Elite" //Name changed for consistency reasons
	States
	{
	Missile:
		ARWV E 16 A_FaceTarget
		ARWV FEF 4 Bright A_CustomMissile("AntaresianPlasma",32,0,0)
		ARWV E 8 A_FaceTarget
		goto See
	}
}

Actor Antaresia : BaronOfHell
{
	Health 575 //575 in orig. DECORATE, but 650 in orig. DEHACKED, 800 elite
	Speed 16
	PainChance 50 //50 in orig. DECORATE, 30 in orig. DEHACKED, 8 for elite
	SeeSound "antaresia/sight"
	PainSound "antaresia/pain"
	DeathSound "antaresia/death"
	Obituary "An antaresia took %o's soul again."
	HitObituary "%o was perforated by an antaresia."
	Tag "Antaresian"
	States
	{
	Spawn:
		ARBS A 10 A_Look
		goto Spawn
	See:
		ARBS ABCD 3 A_Chase
		goto See
	Pain:
		ARBS E 2 
		ARBS E 2 A_Pain
		goto See
	Melee:
	Missile:
		ARBS E 12 Bright A_FaceTarget
		ARBS FFF 4 Bright A_HeadAttack
		ARBS F 8 Bright A_FaceTarget
		ARBS FF 0 Bright A_CustomMissile("AntaresianPlasma",32,0,0)
		ARBS F 6 Bright A_CustomMissile("AntaresianPlasma",32,0,0)
		goto See
	Death:
		ARBS G 5 
		ARBS H 5 A_Scream
		ARBS I 5
		ARBS J 5 A_Fall
		ARBS KL 5
		ARBS M -1 
		stop
	Raise:
		ARBS MLKJIHG 2 
		goto See
	}
}