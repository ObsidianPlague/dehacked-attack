Actor EviternityPlasma : ArachnotronPlasma
{
  Damage 4
  Speed 28
  SeeSound "eviternity/plasma"
  States
  {
  Spawn:
    EPLS AB 5 Bright
    Loop
  Death:
    EPBX ABCDE 5 Bright
    Stop
  }
}

Actor FormerCaptain
{
	Health 80
	Speed 8
	Radius 16
	Height 56
	PainChance 127
	Mass 100
	Monster
	+FLOORCLIP
	SeeSound "chainguy/sight"
	PainSound "chainguy/pain"
	DeathSound "chainguy/death"
	ActiveSound "chainguy/active"
	Obituary "%o was destroyed by a former captain."
	Tag "Former Captain"
	Dropitem "Cell" //Changed for consistency
	States
	{
	Spawn:
		ARWV AABB 10 A_Look
		goto Spawn
	See:
		ARWV AABBCCDD 2 A_Chase
		goto See
	Pain:
		ARWV G 3 
		ARWV G 3 A_Pain
		goto See
	Missile:
		ARWV E 16 A_FaceTarget
		ARWV FEFEF 4 Bright A_CustomMissile("EviternityPlasma",32,0,0)
		ARWV E 8 A_FaceTarget
		goto See
	Death:
		ARWV H 5 
		ARWV I 5 A_Scream
		ARWV J 5 A_Fall
		ARWV KLM 5 
		ARWV N -1 
		stop
	XDeath:
		ARWV O 5 
		ARWV P 5 A_XScream
		ARWV Q 5 A_Fall
		ARWV RST 5 
		ARWV U -1 
		stop
	Raise:
		ARWV NMLKJIH 5 
		goto See
	}
}

Actor NightmareDemon
{
	Health 300
	Speed 15
	Radius 30
	Height 56
	PainChance 100
	Mass 400
	Monster
	+FLOORCLIP
	SeeSound "nightmaredemon/sight"
	AttackSound "nightmaredemon/attack"
	PainSound "nightmaredemon/pain"
	DeathSound "nightmaredemon/death"
	ActiveSound "nightmaredemon/active"
	Obituary "%o was crushed by a nightmare demon."
	Tag "Nightmare Demon"
	States
	{
	Spawn:
		SHSG AABB 10 A_Look
		goto Spawn
	See:
		SHSG AABBCCDD 2 Fast A_Chase
		goto See
	Melee:
		SHSG E 6 A_FaceTarget
		SHSG F 7 A_FaceTarget
		SHSG G 8 A_SargAttack
		goto See
	Pain:
		SHSG H 2
		SHSG H 2 A_Pain
		goto See
	Death:
		SHSG I 8
		SHSG J 8 A_Scream
		SHSG K 4
		SHSG L 4
		SHSG M 4
		SHSG N -1
		stop
	}
}

Actor NightmareSpectre : Demon //PSX Doom, not Eviternity, but included anyway!
{
	Health 300
	Speed 15
	SeeSound "nightmaredemon/sight"
	AttackSound "nightmaredemon/attack"
	PainSound "nightmaredemon/pain"
	DeathSound "nightmaredemon/death"
	ActiveSound "nightmaredemon/active"
	RenderStyle Subtract
  	Alpha 0.95
  	Obituary "%o was eaten by a nightmare spectre."
	Tag "Nightmare Spectre"
}

Actor AstralCaco : Cacodemon
{
	PainChance 20
	Monster
	SeeSound "astralcaco/sight"
	PainSound "astralcaco/pain"
	DeathSound "astralcaco/death"
	Obituary "%o was burned down by an Astral Cacodemon."
	Tag "Astral Cacodemon"
	States
	{
	Spawn:
		ASTR A 10 A_Look
		goto Spawn
	See:
		ASTR A 3 A_Chase
		goto See
	Pain:
		ASTR H 3 
		ASTR I 3 A_Pain
		goto See
	Missile:
		ASTR A 1 A_FaceTarget
		ASTR E 1 A_PlaySoundEx ("astralcaco/active", Auto, False, 0)
		ASTR EEEEEEE 1 A_Chase
		ASTR F 5 A_FaceTarget
		ASTR G 0 A_FatAttack3
		ASTR G 3 A_HeadAttack
		goto See
	Death:
		ASTR J 8 
		ASTR K 8 A_Scream
		ASTR LMNO 8 
		ASTR P 8 
		ASTR Q 8 A_Fall
		ASTR R 8 
		ASTR S -1 
		stop
	Raise:
		ASTR SQPOMLK 5 
		goto See
	}
}

Actor EviternityAnnihilator : BaronOfHell
{
	Health 1100
	Speed 10
	PainChance 20
	+MISSILEMORE //Added just because I felt like being a jerk
	+DONTHARMCLASS //Added to avoid rocket infights within the same class
	SeeSound "monster/brusit" 
	PainSound "baron/pain" 
	DeathSound "monster/brudth"
	MeleeSound "baron/melee"
	Obituary "%o underestimated the firepower of an Annihilator."
	Tag "Annihilator"
	States
	{
	Spawn:
		EBOS AB 10 A_Look
		goto Spawn
	See:
		EBOS AABBCCDD 3 A_Chase
		goto See
	Pain:
		EBOS J 3 
		EBOS J 3 A_Pain
		goto See
	Missile:
		EBOS E 8 A_FaceTarget
		EBOS F 12 Bright A_CyberAttack
		EBOS GS 10 A_FaceTarget
		EBOS IHIHIHI 4 Bright A_SPosAttack
		EBOS S 10 Bright A_FaceTarget
		goto See
	Death:
		EBOS K 8 
		EBOS L 8 A_Scream
		EBOS M 8 A_Fall
		EBOS NOPQ 8 
		EBOS R -1 A_KeenDie
		stop
	Raise:
		EBOS RQPONMLK 5 
		goto See
	}
}

