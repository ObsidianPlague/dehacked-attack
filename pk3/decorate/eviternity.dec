Actor EviternityPlasma : ArachnotronPlasma
{
  Damage 4
  Speed 28
  SeeSound "eviternity/plasma"
  States
  {
  Spawn:
    EPLS AB 5 Bright
    Loop
  Death:
    EPBX ABCDE 5 Bright
    Stop
  }
}

Actor FormerCaptain
{
	Health 80
	Speed 8
	Radius 16
	Height 56
	PainChance 127
	Mass 100
	Monster
	+FLOORCLIP
	SeeSound "chainguy/sight"
	PainSound "chainguy/pain"
	DeathSound "chainguy/death"
	ActiveSound "chainguy/active"
	Obituary "%o was destroyed by a former captain."
	Tag "Former Captain"
	Dropitem "Cell" //Changed for consistency
	States
	{
	Spawn:
		EVWV AABB 10 A_Look
		goto Spawn
	See:
		EVWV AABBCCDD 2 A_Chase
		goto See
	Pain:
		EVWV G 3 
		EVWV G 3 A_Pain
		goto See
	Missile:
		EVWV E 16 A_FaceTarget
		EVWV FEFEF 4 Bright A_CustomMissile("EviternityPlasma",32,0,0)
		EVWV E 8 A_FaceTarget
		goto See
	Death:
		EVWV H 5 
		EVWV I 5 A_Scream
		EVWV J 5 A_Fall
		EVWV KLM 5 
		EVWV N -1 
		stop
	XDeath:
		EVWV O 5 
		EVWV P 5 A_XScream
		EVWV Q 5 A_Fall
		EVWV RST 5 
		EVWV U -1 
		stop
	Raise:
		EVWV NMLKJIH 5 
		goto See
	}
}

Actor NightmareDemon
{
	Health 300
	Speed 15
	Radius 30
	Height 56
	PainChance 100
	Mass 400
	Monster
	+FLOORCLIP
	SeeSound "nightmaredemon/sight"
	AttackSound "nightmaredemon/attack"
	PainSound "nightmaredemon/pain"
	DeathSound "nightmaredemon/death"
	ActiveSound "nightmaredemon/active"
	Obituary "%o was crushed by a nightmare demon."
	Tag "Nightmare Demon"
	States
	{
	Spawn:
		EVSG AABB 10 A_Look
		goto Spawn
	See:
		EVSG AABBCCDD 2 Fast A_Chase
		goto See
	Melee:
		EVSG E 6 A_FaceTarget
		EVSG F 7 A_FaceTarget
		EVSG G 8 A_SargAttack
		goto See
	Pain:
		EVSG H 2
		EVSG H 2 A_Pain
		goto See
	Death:
		EVSG I 8
		EVSG J 8 A_Scream
		EVSG K 4
		EVSG L 4
		EVSG M 4
		EVSG N -1
		stop
	}
}

Actor NightmareSpectre : Demon //PSX Doom, not Eviternity, but included anyway!
{
	Health 300
	Speed 15
	SeeSound "nightmaredemon/sight"
	AttackSound "nightmaredemon/attack"
	PainSound "nightmaredemon/pain"
	DeathSound "nightmaredemon/death"
	ActiveSound "nightmaredemon/active"
	RenderStyle Subtract
  	Alpha 0.95
  	Obituary "%o was eaten by a nightmare spectre."
	Tag "Nightmare Spectre"
}

Actor AstralCaco : Cacodemon
{
	PainChance 20
	Monster
	SeeSound "astralcaco/sight"
	PainSound "astralcaco/pain"
	DeathSound "astralcaco/death"
	Obituary "%o was burned down by an Astral Cacodemon."
	Tag "Astral Cacodemon"
	States
	{
	Spawn:
		ASTR A 10 A_Look
		goto Spawn
	See:
		ASTR A 3 A_Chase
		goto See
	Pain:
		ASTR H 3 
		ASTR I 3 A_Pain
		goto See
	Missile:
		ASTR A 1 A_FaceTarget
		ASTR E 1 A_PlaySoundEx ("astralcaco/active", Auto, False, 0)
		ASTR EEEEEEE 1 A_Chase
		ASTR F 5 Bright A_FaceTarget
		ASTR G 0 Bright A_FatAttack3
		ASTR G 3 Bright A_HeadAttack
		goto See
	Death:
		ASTR J 8 
		ASTR K 8 A_Scream
		ASTR LMNO 8 
		ASTR P 8 
		ASTR Q 8 A_Fall
		ASTR R 8 
		ASTR S -1 
		stop
	Raise:
		ASTR SQPOMLK 5 
		goto See
	}
}

ACTOR EviternityArachnotron : Arachnotron
{
	States
	{
	Missile:
		BSPI A 20 Bright A_FaceTarget
		BSPI G 4 Bright A_CustomMissile("EviternityPlasma")
		BSPI H 4 Bright
		BSPI H 1 Bright A_SpidRefire
		Goto Missile+1
	}
}

Actor EviternityAnnihilator : BaronOfHell
{
	Health 1100
	Speed 10
	PainChance 20
	+DONTHARMCLASS //Added to avoid rocket infights within the same class
	SeeSound "monster/brusit" 
	PainSound "baron/pain" 
	DeathSound "monster/brudth"
	MeleeSound "baron/melee"
	Obituary "%o underestimated the firepower of an Annihilator."
	Tag "Annihilator"
	States
	{
	Spawn:
		EBOS AB 10 A_Look
		goto Spawn
	See:
		EBOS AABBCCDD 3 A_Chase
		goto See
	Pain:
		EBOS J 3 
		EBOS J 3 A_Pain
		goto See
	Missile:
		EBOS E 8 A_FaceTarget
		EBOS F 12 Bright A_CyberAttack
		EBOS GS 10 A_FaceTarget
		EBOS IHIHIHI 4 Bright A_SPosAttack
		EBOS S 10 Bright A_FaceTarget
		goto See
	Death:
		EBOS K 8 
		EBOS L 8 A_Scream
		EBOS M 8 A_Fall
		EBOS NOPQ 8 
		EBOS R -1 A_KeenDie
		stop
	Raise:
		EBOS RQPONMLK 5 
		goto See
	}
}

Actor Archangelus
{
	Radius 48
	Height 80
	Mass 1000
	+SOLID
	States
	{
	Spawn:
		ANGL D 1
		ANGL D 1 ACS_NamedExecute("AngelTag", 0, 0, 0, 0)
		goto Idle
	Idle:
		ANGL T 2 A_Look
		goto Idle
	See:
		ANGL T 240 A_PlaySoundEx ("archangelus/sight", 0, False, 1)
		ANGL [ 8
		ANGL \ 8
		ANGL ] 8
		ANGL A 0 A_Fall
		ANGL A 1 A_SpawnItem ("ArchangelusA", 8, 0)
		stop
	}
}

Actor ArchangelusA
{
	Health 10000
	Speed 24
	Radius 44
	Height 72
	PainChance 5
	Mass 5000
	Monster
	+NOGRAVITY
	+FLOAT
	BloodColor 255 221 000
	SeeSound ""
	PainSound ""
	DeathSound ""
	Obituary "%o was destroyed by Archangelus."
	Tag "Archangelus (Phase I)"
	States
	{
	Spawn:
		ANGL A 2 A_Look
		goto Spawn
	See:
		ANGL AABBCCDD 2 A_Chase
		goto See
	Pain:
		ANGL EJK 8 Bright A_FaceTarget
		ANGL IIII 0 Bright A_BrainSpit("ArchangelusSpawnShot")
		ANGL I 8 Bright A_BrainSpit("ArchangelusSpawnShot")
		goto See
	Missile:
		ANGL FG 6 A_FaceTarget
		ANGL H 0 A_FatAttack1
		ANGL H 0 A_FatAttack2
		ANGL H 6 A_FatAttack3
		ANGL H 0 A_Jump (128, "Archie")
		ANGL JK 6 A_FaceTarget
		ANGL HHHHHHHH 1 A_CustomMissile("EviternityPlasma", 32)
		ANGL I 10 A_FaceTarget
		goto See
	Archie:
		ANGL J 0 Bright A_VileStart
		ANGL J 10 Bright A_PlaySoundEx ("archangelus/pain", Auto, False, 1)
		ANGL J 40 Bright A_VileTarget
		ANGL KH 8 Bright A_FaceTarget
		ANGL I 12 Bright A_VileAttack
		ANGL JK 6 A_FaceTarget
		ANGL HHHHHHHH 1 A_CustomMissile("EviternityPlasma", 32)
		ANGL I 10 A_FaceTarget
		goto See
	Death:
		ANGL V 0 A_Fall
		ANGL V 1 A_SpawnItem ("ArchangelusB", 8, 0)
		ANGL U 0
		stop
	}
}

Actor ArchangelusB
{
	Health 5000
	Speed 48
	Radius 36
	Height 72
	Damage 32
	PainChance 4
	Mass 5000
	Monster
	+NOGRAVITY
	+FLOAT
	+BOSS
	BloodColor 255 221 000
	DeathSound "archangelus/death"
	Obituary "%o was ripped and torn by the Archangelus."
	Tag "Archangelus (Phase II)"
	States
	{
	Spawn:
		ANGL V 1 A_Look
		goto Spawn
	See:
		ANGL V 9 A_PlaySoundEx ("archangelus/cube", Auto, False, 1)
		ANGL WXY 9 
		ANGL Z 9 A_KeenDie
		ANGL L 9
		goto See2
	See2:
		ANGL AABBCCDD 2 A_Chase
		goto See2
	Pain:
		ANGL EJK 8 Bright A_FaceTarget
		ANGL IIII 0 Bright A_BrainSpit("ArchangelusSpawnShot")
		ANGL I 8 Bright A_BrainSpit("ArchangelusSpawnShot")
		ANGL AABBCCDD 2 A_Chase
		goto See2
	Missile:
		ANGL FG 6 A_FaceTarget
	Missile2:
		ANGL H 2 Bright A_FatAttack1
		ANGL H 2 Bright A_FatAttack3
		ANGL H 2 Bright A_FatAttack2
		ANGL I 0 A_Jump (176, "Missile2")
		goto Missile3
	Missile3:
		ANGL JK 6 A_FaceTarget
		goto Missile4
	Missile4:
		ANGL H 0 Bright A_CustomMissile("EviternityPlasma", 32)
		ANGL H 1 Bright A_Chase
		ANGL I 0 A_Jump (248, "Missile4")
		goto Archie
	Archie:
		ANGL J 0 Bright A_VileStart
		ANGL J 10 Bright A_PlaySoundEx ("archangelus/pain", Auto, False, 1)
		ANGL J 40 Bright A_VileTarget
		ANGL KH 8 Bright A_FaceTarget
		ANGL I 12 Bright A_VileAttack
		ANGL JK 6 A_FaceTarget
		ANGL HHHHHHHH 1 A_CustomMissile("EviternityPlasma", 32)
		ANGL I 10 A_FaceTarget
		goto See2
	Death:
		ANGL L 8 Bright A_PlaySoundEx ("archangelus/death", Auto, False, 1)
		ANGL M 8 Bright A_Fall
		ANGL NO 8 Bright 
		ANGL P 4 Bright A_Mushroom ("FatShot", 0, MSF_Classic, 1.0, 0.5)
		ANGL P 4 Bright A_Mushroom ("EviternityPlasma", 0, MSF_Classic, 0.4, 1.25)
		ANGL Q 4 Bright A_Mushroom ("FatShot", 0, MSF_Classic, 2.0, 0.5)
		ANGL Q 4 Bright A_Mushroom ("EviternityPlasma", 0, MSF_Classic, 0.8, 1.25)
		ANGL R 4 Bright A_Mushroom ("FatShot", 0, MSF_Classic, 3.0, 0.5)
		ANGL R 4 Bright A_Mushroom ("EviternityPlasma", 0, MSF_Classic, 1.2, 1.25)
		ANGL S 4 Bright A_Mushroom ("FatShot", 0, MSF_Classic, 4.0, 0.5)
		ANGL U 224 Bright 
		ANGL U -1 Bright A_BrainDie
		stop
	}
}
ACTOR ArchangelusSpawnShot : SpawnShot
{
  DropItem "DEHDoomImp" 	255 50
  DropItem "DEHDemon"		255 40
  DropItem "FormerCaptain"	255 30
  DropItem "AstralCaco"		255 10
  DropItem "DEHCacodemon"	255 30
  DropItem "NightmareDemon"	255 20
  DropItem "EviternityArachnotron"	255 20
  DropItem "DEHFatso"			255 20
  DropItem "DEHHellKnight"		255 24
  DropItem "EviternityAnnihilator"	255 12
  Speed 30
  SeeSound "archangelus/spit"
  DeathSound "brain/cubeboom"
  States
  {
  Spawn:
    EBSF A 3 Bright A_SpawnFly("ArchangelusSpawnFire")
	Loop
  }
}
ACTOR ArchangelusSpawnFire : SpawnFire
{
  States
  {
  Spawn:
    EBSF BCDE 4 Bright A_Fire
	Stop
  }
}