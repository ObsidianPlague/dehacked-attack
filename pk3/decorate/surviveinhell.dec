ACTOR Belphegor : BaronOfHell
{
   Health 1500
   Obituary "%o was incinerated by a Belphegor."
   HitObituary "the last thing %o saw was the Belphegor's abs."
   Tag "Belphegor"
   States
   {
   Spawn:
      BOS3 AB 10 A_Look
      Loop
   See:
      BOS3 AABBCCDD 3 A_Chase
      Loop
   Melee:
   Missile:
      BOS3 EF 6 A_FaceTarget
      BOS3 G 6 A_BruisAttack
      BOS3 F 6 A_FaceTarget
      BOS3 G 6 A_BruisAttack
      Goto See
   Pain:
      BOS3 H 2
      BOS3 H 2 A_Pain
      Goto See
   Death:
      BOS3 I 8
      BOS3 J 8 A_Scream
      BOS3 K 8
      BOS3 L 8 A_NoBlocking
      BOS3 MN 8
      BOS3 O -1
      Stop
   Raise:
      BOS3 ONMLKJI 8
      Goto See
   }
}

ACTOR PoisonSoul
{
Health 500
Radius 16
Height 56
Mass 50
Speed 8
RENDERSTYLE SoulTrans
PAINCHANCE 255
MONSTER
+NOGRAVITY
+FLOAT
+dontfall
AttackSound "skull/melee"
PainSound "skull/pain"
DeathSound "skull/death"
ActiveSound "skull/active"
Tag "Poison Soul"
Obituary "%o was poisoned by a Poison Soul."
HitObituary "%o was chomped on by a Poison Soul."
States
   {
   Spawn:
       SKLL A 10 A_Look
       Loop
   See:
       SKLL A 3 A_Chase
       Loop
   Missile:
       SKLL B 10 A_FaceTarget
       SKLL B 4 A_BruisAttack
       Goto See
   Pain:
       SKLL C 3
       SKLL C 3 A_Pain
       Goto See
   Death:
       SKLL D 6
       SKLL E 6 A_Scream
       SKLL F 6
       SKLL G 6 A_Noblocking
       SKLL H 6
       Stop
   } 
}

ACTOR HellSmith
{
  Health 8000 //Change: 20000 --> 8000 health for balance reasons
  Radius 40
  Height 110
  Mass 1000
  Speed 24
  PainChance 0
  Monster
  MinMissileChance 160
  +BOSS //Added
  +MISSILEMORE //Added
  +MISSILEEVENMORE //Added
  +FLOORCLIP
  +DONTMORPH
  +BOSSDEATH
  Obituary "%o was incinerated by a Hell Smith."
  SeeSound ""
  PainSound "brain/pain"
  DeathSound ""
  ActiveSound "cyber/active"
  Tag "Hell Smith"
  States
  {
  Spawn:
    AVTR A 10 A_Look
    Loop
  See:
    AVTR AB 6 A_Chase
    Loop
  Missile:
    AVTR C 6 A_FaceTarget //Change: Doubled firing times to be less mean.
    AVTR D 2 A_FatAttack3
    AVTR D 2 A_FatAttack2
    AVTR D 2 A_FatAttack1
    AVTR E 2 A_FatAttack1
    AVTR E 2 A_FatAttack2
    AVTR E 2 A_FatAttack3
    Goto See
  Pain:
    AVTR F 10 A_Pain
    Goto See
  Death:
    AVTR G 10
    AVTR H 10 A_Scream
    AVTR IJK 10
    AVTR L 10 A_NoBlocking
    AVTR MNO 10
    AVTR P 30
    AVTR P -1 A_BossDeath
    Stop
  }
}