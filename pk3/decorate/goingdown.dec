ACTOR AngryMastermind : SpiderMastermind
{
  Health 6000 //Change: 12000 --> 4500 health for balance reasons
  -NORADIUSDMG //Deliberate decision to keep defensive ability thematic
  Obituary "%o stood in awe of the angry spiderdemon."
  Tag "Angry Spiderdemon"
  PainSound "brain/pain"
  States
  {
  Spawn:
    RSPD AB 10 Bright A_Look
    TNT1 A 0 A_JumpIf(ACS_NamedExecuteWithResult("Dehacked_BossCheck") == 4, "Reset")
    Goto Idle
  Idle:
    RSPD AB 10 Bright A_Look
    Loop
  Reset:
    TNT1 A 0 A_ChangeFlag("COUNTKILL", 0)
    TNT1 A 0 A_SpawnItemEx("SpiderMastermind", 0, 0, 0, 0, 0, 0, 0, SXF_TRANSFERAMBUSHFLAG|SXF_TRANSFERPOINTERS|SXF_NOCHECKPOSITION, 0)
    Stop
  See:
    RSPD A 3 A_Metal
    RSPD ABB 3 A_Chase
    RSPD C 3 A_Metal
    RSPD CDD 3 A_Chase
    RSPD C 3 A_Metal
    RSPD CBB 3 A_Chase
    Loop
  Missile:
    RSPD A 10 Bright A_FaceTarget
    RSPD EG 4 Bright A_SPosAttackUseAtkSound
    RSPD E 1 Bright A_SpidRefire
    Goto Missile+1
  Pain: //Change: I don't know the frame numbers, so these are probably wrong.
    RSPD H 3
    RSPD H 3 A_Pain
    RSPD A 10 Bright A_FaceTarget
    RSPD E 4 Bright A_FatAttack1
    RSPD G 4 Bright A_FatAttack2
    RSPD E 4 Bright A_SkelMissile
    RSPD G 4 Bright A_SkelMissile
    Goto See
  Death:
    RSPD I 20 A_Scream
    RSPD N 10 A_NoBlocking
    RSPD OPQRSTU 10
    RSPD V 30
    RSPD V -1 A_BossDeath
    Stop
  }
}

ACTOR AngryMastermindFinalPhase1 : SpiderMastermind
{
  DropItem "AngryMastermindFinalPhase2"
  var int user_count;
  States
  {
  Death:
	SPID A 0 A_SetUserVar("user_count", ACS_NamedExecuteWithResult("Dehacked_BossTargetCount"))
	Goto SpawnerLoop1
  Death1:
	SPID J 10 A_Scream
	SPID JKL 10
	SPID A 0 A_SetUserVar("user_count", ACS_NamedExecuteWithResult("Dehacked_BossTargetCount"))
	Goto SpawnerLoop2
  Death2:
	SPID MNOP 10
	SPID A 0 A_SetUserVar("user_count", ACS_NamedExecuteWithResult("Dehacked_BossTargetCount"))
	Goto SpawnerLoop3
  Death3:
	SPID QRSS 10
	SPID A 0 A_SetUserVar("user_count", ACS_NamedExecuteWithResult("Dehacked_BossTargetCount"))
	Goto SpawnerLoop4
  DeathFinal:
    SPID S 10 A_NoBlocking
	SPID S -1
    Stop
  SpawnerLoop1:
	TNT1 A 0 A_BrainSpit("GDSpawnShot")
	TNT1 A 0 A_SetUserVar("user_count", user_count - 1)
	TNT1 A 0 A_JumpIf(user_count <= 0, "Death1")
	Loop
  SpawnerLoop2:
	TNT1 A 0 A_BrainSpit("GDSpawnShot")
	TNT1 A 0 A_SetUserVar("user_count", user_count - 1)
	TNT1 A 0 A_JumpIf(user_count <= 0, "Death2")
	Loop
  SpawnerLoop3:
	TNT1 A 0 A_BrainSpit("GDSpawnShot")
	TNT1 A 0 A_SetUserVar("user_count", user_count - 1)
	TNT1 A 0 A_JumpIf(user_count <= 0, "Death3")
	Loop
  SpawnerLoop4:
	TNT1 A 0 A_BrainSpit("GDSpawnShot")
	TNT1 A 0 A_SetUserVar("user_count", user_count - 1)
	TNT1 A 0 A_JumpIf(user_count <= 0, "DeathFinal")
	Loop
  }
}
ACTOR GDSpawnShot : SpawnShot
{
  Speed 30
  DropItem "DoomImp", 256, 64
  DropItem "Zombieman", 256, 32
  DropItem "Shotgunguy", 256, 32
  States
  {
  Spawn:
    TNT1 A 1 BRIGHT A_SpawnItemEx("DEHSpawnShotFog",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION,0)
    TNT1 A 1 BRIGHT A_SpawnFly("TeleportFog")
    Loop
  }
}
ACTOR AngryMastermindFinalPhase2 : AngryMastermind
{
  Health 12000
  +MISSILEEVENMORE
  States
  {
  Spawn:
    RSPD AB 10 Bright A_Look
	Goto Idle
  Idle:
    RSPD AB 10 Bright A_Look
    Loop
  Pain: //Change: I don't know the frame numbers, so these are probably wrong.
    RSPD H 3
    RSPD H 3 A_BrainPain
    RSPD A 10 Bright A_FaceTarget
    RSPD E 4 Bright A_FatAttack1
    RSPD G 4 Bright A_FatAttack2
    RSPD E 4 Bright A_SkelMissile
    RSPD G 4 Bright A_SkelMissile
    Goto See
  Death:
    RSPD I 20 A_PlaySound("brain/death", 0, 1.0, 0, ATTN_NONE)
    RSPD N 10 A_NoBlocking
    RSPD OPQRSTU 10
    RSPD V 30
    RSPD V -1 A_BrainDie
    Stop
  }
}