ACTOR StruggleClass : DEHBoolean {}
ACTOR JohnStruggle : DoomPlayer
{
  Player.DisplayName "Doomguy (Struggle: Antaresian Legacy)"
  Player.StartItem "StrugglePistol"
  Player.StartItem "StruggleHammer"
  Player.StartItem "Clip", 42
  Player.StartItem "StruggleClass"
/*
  Player.WeaponSlot 1, StruggleDrillMachine, StruggleHammer
  Player.WeaponSlot 2, StrugglePistol
  Player.WeaponSlot 3, StruggleSuperShotgun, StruggleShotgun
  Player.WeaponSlot 4, StruggleDualPistols
  Player.WeaponSlot 5, StruggleGrenadeLauncher
  Player.WeaponSlot 6, StruggleAssaultRifle
  Player.WeaponSlot 7, StruggleLeichenfaust
*/
}

ACTOR StruggleClip : Clip
{
  Inventory.PickupMessage "Picked up a 9mm clip."
  Inventory.Amount 12
  States
  {
  Spawn:
    STBL A -1
    Stop
  }
}
ACTOR StruggleClipBox : Clip
{
  Inventory.PickupMessage "Picked up a box of 9mm bullets."
  Inventory.Amount 60
  States
  {
  Spawn:
    STBB A -1
    Stop
  }
}
ACTOR StruggleShell : Shell
{
  Inventory.PickupMessage "Picked up a pair of shells."
  Inventory.Amount 2
  States
  {
  Spawn:
    STSH A -1
    Stop
  }
}
ACTOR StruggleShellBox : Shell
{
  Inventory.PickupMessage "Picked up a small box of shells."
  Inventory.Amount 10
  States
  {
  Spawn:
    STSB A -1
    Stop
  }
}
ACTOR StruggleRocketAmmo : RocketAmmo
{
  Inventory.PickupMessage "Picked up a grenade."
  Inventory.Amount 1
  States
  {
  Spawn:
    STRO A -1
    Stop
  }
}
ACTOR StruggleRocketBox : RocketAmmo
{
  Inventory.PickupMessage "Picked up a box of grenades."
  Inventory.Amount 5
  States
  {
  Spawn:
    STRB A -1
    Stop
  }
}
ACTOR 223CaliberMagazine : Ammo
{
  Inventory.PickupMessage "Picked up a .223 caliber magazine."
  Inventory.Amount 20
  Inventory.MaxAmount 160
  Ammo.BackpackAmount 20
  Ammo.BackpackMaxAmount 320
  Inventory.Icon "STCLA0"
  Inventory.Amount 20
  States
  {
  Spawn:
    STCL A -1
    Stop
  }
}
ACTOR 223CaliberMagazineBox : 223CaliberMagazine
{
  Inventory.PickupMessage "Picked up a box of .223 caliber magazines."
  Inventory.Amount 100
  States
  {
  Spawn:
    STCP A -1
    Stop
  }
}

ACTOR StruggleHammer : DoomWeapon
{
  Weapon.SelectionOrder 3700
  Weapon.Kickback 100
  Obituary "$OB_MPFIST"
  Tag "Hammer"
  +WEAPON.WIMPY_WEAPON
  +WEAPON.MELEEWEAPON
  States
  {
  Ready:
    SPUN A 1 A_WeaponReady
    Loop
  Deselect:
    SPUN A 1 A_Lower
    Loop
  Select:
    PUNG A 1 A_Raise
    Loop
  Fire:
    SPUN B 2
    SPUN CC 0 A_Punch
    SPUN C 12 A_Punch
    SPUN D 6
    SPUN B 6
    SPUN A 4 A_ReFire
    Goto Ready
  }
}

ACTOR StruggleDrillMachine : DoomWeapon
{
  Weapon.Kickback 0
  Weapon.SelectionOrder 2200
  Weapon.UpSound "struggle/sawup"
  Weapon.ReadySound "struggle/sawidle"
  Inventory.PickupMessage "You got a drill machine...what?"
  Inventory.PickupSound "struggle/w_pkup"
  Obituary "$OB_MPCHAINSAW"
  Tag "Drill Machine"
  +WEAPON.MELEEWEAPON
  States
  {
  Ready:
    STSW C 1 A_WeaponReady
    Loop
  Deselect:
    STSW C 1 A_Lower
    Loop
  Select:
    STSW C 1 A_Raise
    Loop
  Fire:
    STSW AB 2 A_Saw("struggle/sawfull", "struggle/sawhit", 2)
    STSW B 0 A_ReFire
    Goto Ready
  Spawn:
    STCS A -1
    Stop
  }
}

ACTOR StrugglePistol : DoomWeapon
{
  Weapon.SelectionOrder 1900
  Weapon.AmmoUse 1
  Weapon.AmmoGive 24
  Weapon.AmmoType "Clip"
  Obituary "$OB_MPPISTOL"
  +WEAPON.WIMPY_WEAPON
  Inventory.PickupMessage "$PICKUP_PISTOL_DROPPED"
  Inventory.PickupSound "struggle/w_pkup"
  Tag "Machine Pistol"
  States
  {
  Ready:
    SPSG A 1 A_WeaponReady
    Loop
  Deselect:
    SPSG A 1 A_Lower
    Loop
  Select:
    SPSG A 1 A_Raise
    Loop
  Fire:
    SPSG A 2
    SPSG B 0 A_FireBullets(5.6, 0, 1, 5, "BulletPuff")
    SPSG B 0 A_PlaySound("struggle/pistol", CHAN_WEAPON)
    SPSG B 3 A_GunFlash
    SPSG C 0 A_FireBullets(5.6, 0, 1, 5, "BulletPuff")
    SPSG C 0 A_PlaySound("struggle/pistol", CHAN_WEAPON)
    SPSG C 3 A_GunFlash
    SPSG C 2
    SPSG B 4
    SPSG B 5 A_ReFire
    Goto Ready
  Flash:
    SPSF A 1 Bright A_Light1
    SPSF A 1 Bright A_Light2
    SPSF B 1 Bright A_Light1
    Goto LightDone
  Spawn:
    PIST A -1
    Stop
  }
}

ACTOR StruggleShotgun : DoomWeapon
{
  Weapon.SelectionOrder 1300
  Weapon.AmmoUse 1
  Weapon.AmmoGive 4
  Weapon.AmmoType "Shell"
  Inventory.PickupMessage "$GOTSHOTGUN"
  Inventory.PickupSound "struggle/w_pkup"
  Obituary "$OB_MPSHOTGUN"
  Tag "$TAG_SHOTGUN"
  States
  {
  Ready:
    STSG A 1 A_WeaponReady
    Loop
  Deselect:
    STSG A 1 A_Lower
    Loop
  Select:
    STSG A 1 A_Raise
    Loop
  Fire:
    STSG A 3
    STSG A 0 A_FireBullets(5.6, 0, 7, 5, "BulletPuff")
    STSG A 0 A_PlaySound("struggle/shotgf", CHAN_WEAPON)
    STSG A 7 A_GunFlash
    STSG BCDC 4
    STSG BA 3
    STSG A 6 A_ReFire
    Goto Ready
  Flash:
    STSF A 4 Bright A_Light1
    STSF B 3 Bright A_Light2
    Goto LightDone
  Spawn:
    STSO A -1
    Stop
  }
}

ACTOR StruggleSuperShotgun : DoomWeapon
{
  Weapon.SelectionOrder 400
  Weapon.AmmoUse 2
  Weapon.AmmoGive 8
  Weapon.AmmoType "Shell"
  Inventory.PickupMessage "$GOTSHOTGUN2"
  Inventory.PickupSound "struggle/w_pkup"
  Obituary "$OB_MPSSHOTGUN"
  Tag "$TAG_SUPERSHOTGUN"
  States
  {
  Ready:
    SHT2 A 1 A_WeaponReady
    Loop
  Deselect:
    SHT2 A 1 A_Lower
    Loop
  Select:
    SHT2 A 1 A_Raise
    Loop
  Fire:
    SHT2 A 3
    SHT2 A 0 A_FireBullets(11.2, 7.1, 20, 5, "BulletPuff")
    SHT2 A 0 A_PlaySound("struggle/sshotf", CHAN_WEAPON)
    SHT2 A 7 A_GunFlash
    STSS B 7
    STSS C 7 A_CheckReload
    STSS D 7 A_OpenShotgun2
    STSS E 7
    STSS F 7 A_LoadShotgun2
    STSS G 6
    STSS H 6 A_CloseShotgun2
    SHT2 A 5 A_ReFire
    Goto Ready
  Flash:
    SHT2 I 4 Bright A_Light1
    SHT2 J 3 Bright A_Light2
    Goto LightDone
  Spawn:
    SGN2 A -1
    Stop
  }
}

ACTOR StruggleDualPistols : DoomWeapon
{
   Weapon.SelectionOrder 700
   Weapon.AmmoUse 1
   Weapon.AmmoGive 24
   Weapon.AmmoType "Clip"
   Inventory.PickupMessage "Unlocked dual-wield mode!"
   Inventory.PickupSound "struggle/w_pkup"
   Obituary "$OB_MPCHAINGUN"
   Tag "Dual Machine Pistols"
   States
   {
   Ready:
     STCG A 1 A_WeaponReady
     Loop
   Deselect:
     STCG A 1 A_Lower
     Loop
   Select:
     STCG A 1 A_Raise
     Loop
   Fire:
     STCG B 0 A_PlaySound("struggle/chngun", CHAN_WEAPON)
     STCG B 0 A_GunFlash
     STCG B 3 Bright A_FireBullets(5.6, 0, 1, 5, "BulletPuff")
     STCG C 0 A_PlaySound("struggle/chngun", CHAN_WEAPON)
     STCG C 0 A_GunFlash
     STCG C 3 Bright A_FireBullets(5.6, 0, 1, 5, "BulletPuff")
     STCG C 0 A_ReFire
     Goto Ready
   Spawn:
     STCH A -1
     Stop
   }
}

ACTOR StruggleGrenadeLauncher : DoomWeapon
{
  Weapon.SelectionOrder 2500
  Weapon.AmmoUse 1
  Weapon.AmmoGive 2
  Weapon.AmmoType "RocketAmmo"
  +WEAPON.NOAUTOFIRE
  Inventory.PickupMessage "You got a grenade launcher!"
  Inventory.PickupSound "struggle/w_pkup"
  Tag "Grenade Launcher"
  States
  {
  Ready:
    STMG A 1 A_WeaponReady
    Loop
  Deselect:
    STMG A 1 A_Lower
    Loop
  Select:
    STMG A 1 A_Raise
    Loop
  Fire:
    STMG B 2 A_GunFlash
    STMG B 33 A_FireCustomMissile("StruggleRocket", 0, 1)
    STMG B 0 A_ReFire
    Goto Ready
  Flash:
    STMF A 4 Bright A_Light1
    STMF B 4 Bright A_Light1
    STMF CB 4 Bright A_Light2
    STMF A 4 Bright A_Light1
    Goto LightDone
  Spawn:
    STLN A -1
    Stop
  }
}
ACTOR StruggleRocket : Rocket
{
  Radius 12
  Speed 30
  Damage 50
  Obituary "%o rode %k's rocket-propelled grenade."
  SeeSound "struggle/rocklf"
  States
  {
  Spawn:
    STMS A 1 Bright
    Loop
  }
}

ACTOR StruggleAssaultRifle : DoomWeapon
{
  Weapon.SelectionOrder 100
  Weapon.AmmoUse 1
  Weapon.AmmoGive 40
  Weapon.AmmoType "223CaliberMagazine"
  Inventory.PickupMessage "You got an assault rifle!"
  Inventory.PickupSound "struggle/w_pkup"
  Tag "Assault Rifle"
  States
  {
  Ready:
    STPG A 1 A_WeaponReady
    Loop
  Deselect:
    STPG A 1 A_Lower
    Loop
  Select:
    STPG A 1 A_Raise
    Loop
  Fire:
    STPG A 0 A_Jump(128, "Fire2")
    STPG A 0 A_GunFlash
    STPG A 3 A_FireCustomMissile("StruggleAssaultRifleShot", 0, 1)
    STPG B 20 A_ReFire
    Goto Ready
  Fire2:
    STPG A 0 A_GunFlash("Flash2")
    STPG A 3 A_FireCustomMissile("StruggleAssaultRifleShot", 0, 1)
    STPG B 20 A_ReFire
    Goto Ready
  Flash:
    STPF A 4 Bright A_Light1
    Goto LightDone
  Flash2:
    STPF B 4 Bright A_Light1
    Goto LightDone
  Spawn:
    STPL A -1
    Stop
  }
}

ACTOR StruggleAssaultRifleShot : PlasmaBall
{
  Radius 11
  Speed 35
  Damage 7
  SeeSound "struggle/plasmaf"
  States
  {
  Spawn:
    STPS AB 2 Bright
    Loop
  Death:
    STPE AB 4 Bright
    Stop
  }
}

ACTOR StruggleLeichenfaust : DoomWeapon
{
  Height 20
  Weapon.SelectionOrder 2800
  Weapon.AmmoUse 6
  Weapon.AmmoGive 2
  Weapon.AmmoType "RocketAmmo"
  +WEAPON.NOAUTOFIRE
  Inventory.PickupMessage "You got a leichenfaust...oh, yeah!"
  Inventory.PickupSound "struggle/w_pkup"
  Tag "Leichenfaust"
  States
  {
  Ready:
    STBG A 1 A_WeaponReady
    Loop
  Deselect:
    STBG A 1 A_Lower
    Loop
  Select:
    STBG A 1 A_Raise
    Loop
  Fire:
    STBG B 2 Bright A_PlaySound("struggle/bfgf", CHAN_WEAPON)
    STBG C 2 Bright A_GunFlash
    STBG D 29 Bright A_FireCustomMissile("StruggleLeichenfaustBall", 0, 1)
    STBG D 4 A_ReFire
    STBG E 4 A_PlaySound("struggle/bfgl", CHAN_WEAPON)
    STBG FGH 4
    Goto Ready
  Flash:
    TNT1 A 4 Bright A_Light1
    TNT1 A 7 Bright A_Light2
    TNT1 A 6 Bright A_Light1
    Goto LightDone
  Spawn:
    STBU A -1
    Stop
  }
}
ACTOR StruggleLeichenfaustBall : BFGBall
{
  Damage 50
  Speed 25
  +ROCKETTRAIL
  States
  {
  Spawn:
    STBF A 4 Bright
    Loop
  Death:
    STFE A 4 Bright A_BFGSpray("LechenfaustExtra")
    STFE BCDE 4 Bright
    Stop
  }
}
ACTOR LechenfaustExtra : BFGExtra
{
  States
  {
  Spawn:
    MISL A 0 Bright
    MISL B 8 Bright A_Explode
    MISL C 6 Bright
    MISL D 4 Bright
    Stop
  }
}

