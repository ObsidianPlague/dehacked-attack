ACTOR JohnStrain : DoomPlayer
{
  Player.DisplayName "STRAIN Doomguy"
  Player.StartItem "StrainDoubleFist"
  Player.StartItem "StrainPistol"
  Player.StartItem "Clip", 0
/*
  Player.WeaponSlot 1, Chainsaw, StrainDoubleFist
  Player.WeaponSlot 2, StrainPistol
  Player.WeaponSlot 3, SuperShotgun, StrainSawedOff
  Player.WeaponSlot 4, Chaingun
  Player.WeaponSlot 5, RocketLauncher
  Player.WeaponSlot 6, StrainNFG
  Player.WeaponSlot 7, StrainPsychicBlaster
*/
}

ACTOR StrainRedArmor : BasicArmorPickup
{
  Radius 20
  Height 16
  Inventory.PickupMessage "$GOTMEGA" // "Picked up the MegaArmor!"
  Inventory.Icon "ARM3A0"
  Armor.SavePercent 50
  Armor.SaveAmount 300
  States
  {
  Spawn:
    ARM3 A 6
    ARM3 B 6 Bright
    Loop
  }
}

ACTOR StrainDoubleFist : Weapon
{
  Weapon.SelectionOrder 3700
  Weapon.Kickback 100
  Obituary "$OB_MPFIST"
  Tag "Double Fist"
  +WEAPON.WIMPY_WEAPON
  +WEAPON.MELEEWEAPON
  States
  {
  Ready:
    PUN2 A 1 A_WeaponReady
    Loop
  Deselect:
    PUN2 A 1 A_Lower
    Loop
  Select:
    PUN2 A 1 A_Raise
    Loop
  Fire:
    PUN2 B 4
    PUN2 C 4 A_Punch
    PUN2 D 5 A_Punch
    PUN2 E 4
    PUN2 A 5 A_ReFire
    Goto Ready
  }
}

ACTOR StrainPistol : DoomWeapon
{
  Weapon.SelectionOrder 1900
  Weapon.AmmoUse 1
  Weapon.AmmoGive 20
  Weapon.AmmoType "Clip"
  Obituary "$OB_MPPISTOL"
  +WEAPON.WIMPY_WEAPON
  Inventory.Pickupmessage "$PICKUP_PISTOL_DROPPED"
  Tag "$TAG_PISTOL"
  States
  {
  Ready:
    PISG A 1 A_WeaponReady
    Loop
  Deselect:
    PISG A 1 A_Lower
    Loop
  Select:
    PISG A 1 A_Raise
    Loop
  Fire:
    PISG A 3
    PISG B 5 A_FirePistol
    PISG C 3
    PISG B 5 A_ReFire
    Goto Ready
  Flash:
    PISF A 6 Bright A_Light1
    Goto LightDone
    PISF A 6 Bright A_Light1
    Goto LightDone
  Spawn:
    PIST A -1
    Stop
  }
}

ACTOR StrainSawedOff : DoomWeapon
{
  Weapon.SelectionOrder 1300
  Weapon.AmmoUse 1
  Weapon.AmmoGive 8
  Weapon.AmmoType "Shell"
  Inventory.PickupMessage "You got the sawed-off!"
  Obituary "$OB_MPSHOTGUN"
  Tag "Sawed-off"
  States
  {
  Ready:
    SG3G A 1 A_WeaponReady
    Loop
  Deselect:
    SG3G A 1 A_Lower
    Loop
  Select:
    SG3G A 1 A_Raise
    Loop
  Fire:
    SG3G A 3
    SG3G A 0 A_FireBullets (5.6, 0, 7, 5, "BulletPuff")
    SG3G A 0 A_PlaySound ("strain/shotgf", CHAN_WEAPON)
    SG3G A 4 A_GunFlash
    SG3G B 5
    SG3G C 4
    SG3G D 5
    SG3G C 4
    SG3G BA 3
    SG3G A 6 A_ReFire
    Goto Ready
  Flash:
    SG3F A 2 Bright A_Light1
    SG3F B 3 Bright A_Light2
    Goto LightDone
  Spawn:
    SGN3 A -1
    Stop
  }
}

ACTOR StrainNFG : DoomWeapon
{
  Weapon.SelectionOrder 100
  Weapon.AmmoUse 1
  Weapon.AmmoGive 12
  Weapon.AmmoType "NFGBattery"
  Inventory.PickupMessage "You got the NFG!!"
  Tag "NFG"
  States
  {
  Ready:
    FFGG A 1 A_WeaponReady
    Loop
  Deselect:
    FFGG A 1 A_Lower
    Loop
  Select:
    FFGG A 1 A_Raise
    Loop
  Fire:
    FFGG A 0 A_GunFlash
    FFGG A 3 A_FireCustomMissile("NFGPlasmaBall", 0, 1)
    FFGG B 40 A_ReFire
    Goto Ready
  Flash:
    FFGF A 4 Bright A_Light1
    Goto LightDone
    FFGF B 4 Bright A_Light1
    Goto LightDone
  Spawn:
    FFGL A -1
    Stop
  }
}
ACTOR NFGPlasmaBall : PlasmaBall
{
  Radius 8
  Height 8
  Speed 40
  Damage 12
  SeeSound "strain/plasmaf"
  DeathSound "weapons/plasmax"
  Obituary "%o was melted by %k's NFG."
  States
  {
  Spawn:
    FFGS AB 6 Bright
    Loop
  Death:
    FFGE ABCDE 4 Bright
    Stop
  }
}

ACTOR NFGBattery : Ammo
{
  Inventory.PickupMessage "Picked up a mini-battery."
  Inventory.Amount 6
  Inventory.MaxAmount 100
  Ammo.BackpackAmount 6
  Ammo.BackpackMaxAmount 200
  Inventory.Icon "BATTA0"
  States
  {
  Spawn:
    BATT A -1
    Stop
  }
}
ACTOR NFGBatteryPack : NFGBattery
{
  Inventory.PickupMessage "Picked up a battery."
  Inventory.Amount 30
  States
  {
  Spawn:
    BATP A -1
    Stop
  }
}

ACTOR StrainPsychicBlaster : DoomWeapon
{
  Height 20
  Weapon.SelectionOrder 2800
  Weapon.AmmoUse 3
  Weapon.AmmoGive 3
  Weapon.AmmoType "RocketAmmo"
  +WEAPON.NOAUTOFIRE
  Inventory.PickupMessage "You got the Psychic Blaster!"
  Tag "Psychic Blaster"
  States
  {
  Ready:
    PSYB A 1 A_WeaponReady
    Loop
  Deselect:
    PSYB A 1 A_Lower
    Loop
  Select:
    PSYB A 1 A_Raise
    Loop
  Fire:
    PSYB A 8 A_PlaySound("strain/bfgf", CHAN_WEAPON)
    PSYB B 8 A_GunFlash
    PSYB B 2 A_FireCustomMissile("StrainPsychicBlasterBall", 0, 1)
    PSYB B 2 A_ReFire
    Goto Ready
  Flash:
    TNT1 A 4 Bright A_Light1
    TNT1 A 4 Bright A_Light2
    TNT1 A 4 Bright A_Light1
    Goto LightDone
  Spawn:
    PSYG A -1
    Stop
  }
}
ACTOR StrainPsychicBlasterBall : BFGBall
{
  Speed 8
  Damage 200
  DeathSound "weapons/bfgx"
  Obituary "%o couldn't hide from %k's psychic blaster."
  RenderStyle OptFuzzy
  States
  {
  Spawn:
    BFS1 AB 4 Bright
    Loop
  Death:
    BFE1 AB 8 Bright
    BFE1 C 8 Bright A_Explode
    BFE1 DEF 8 Bright
    Stop
  }
}