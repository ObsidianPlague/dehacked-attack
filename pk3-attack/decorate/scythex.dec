Actor SpiritImp : DoomImp
{
  Health 125
  Speed 16
  PainChance 170
  Mass 200
  BloodColor "DarkGreen"
  Obituary "%o was killed by a Spirit Imp."
  Tag "Shadow Imp"
  DamageFactor "Extinguishing", 3.0
  States
  {
  Spawn:
    SHTR A 0
	TNT1 A 0 A_JumpIf(ACS_NamedExecuteWithResult("Dehacked_MonsterReset", DEH_SCYTHEX, 10, 12), "Reset")
    Goto Idle
  Reset:
	TNT1 A 0 A_ChangeFlag("COUNTKILL", 0)
	TNT1 A 0 A_SpawnItemEx("DoomImp", 0, 0, 0, 0, 0, 0, 0, SXF_TRANSFERAMBUSHFLAG|SXF_TRANSFERPOINTERS|SXF_NOCHECKPOSITION, 0)
	Stop
  Idle:
    SHTR A 3 A_Look
    Loop
  See:
    SHTR BBDDCCEE 3 A_Chase
    Loop
  Melee:
  Missile:
    SHTR FF 3 A_FaceTarget
    SHTR G 6 A_CustomMissile("ShadowImpBall",32,0,0)
    Goto See
  Pain:
    SHTR H 2
    SHTR H 2 A_Pain
    Goto See
  Death:
    SHTR I 8
    SHTR J 8 A_Scream
    SHTR K 6
    SHTR L 6 A_NoBlocking
    SHTR M -1
    Stop
  Raise:
    SHTR ML 8
    SHTR KJI 6
    Goto See
  }
}
ACTOR ShadowImpBall
{
  Radius 13
  Height 8
  Speed 25
  Damage 5
  Projectile
  +RANDOMIZE
  RenderStyle Add 
  Alpha 0.75
  SeeSound "baron/attack"
  DeathSound "baron/shotx"
  States
  {
  Spawn:
    ACID AB 5 Bright A_SpawnItemEx("ShadowImpBallFX",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION)
    Loop
  Death:
	ACID A 0 A_PainAttack("ShadowSoul")
    ACID CDE 5 Bright
    Stop
  }
}
ACTOR ShadowImpBallFX
{
  +NOINTERACTION
  +CLIENTSIDEONLY
  RenderStyle Add
  Alpha 0.75
  States
  {
  Spawn:
    ACID CDE 5 Bright
    Stop  
  }
}

ACTOR ShadowSoul
{
  Health 5
  Radius 16
  Height 56
  Mass 50
  Speed 7
  PainChance 256
  Monster
  +FLOAT
  +NOGRAVITY
  +DONTFALL
  +NOICEDEATH
  -SOLID
  AttackSound "skull/melee"
  PainSound "skull/pain"
  DeathSound "skull/death"
  ActiveSound "skull/active"
  RenderStyle SoulTrans
  Obituary "%o was spooked by a shadow spirit." // "%o was spooked by a lost soul."
  BloodColor "DarkGreen"
  Tag "Shadow Spirit"
  DamageFactor "Antiair", 3.0 //Like this actually matters at 5 HP
  States
  {
  Spawn:
    SHSP A 10 Bright A_Look
    Loop
  See:
    SHSP AB 3 Bright A_Chase
    Loop
  Missile:
    SHSP AB 3 Bright A_FaceTarget
    SHSP A 0 Bright A_BruisAttack
	Goto See
  Pain:
    SHSP AB 3 Bright
    Goto See
  Death:
    SHSP EFGH 3 Bright A_NoBlocking
    SHSP IJ 3
    Stop
  }
}

ACTOR SpiritElemental : PainElemental
{
  Obituary "%o was killed by a Spirit Elemental."
  BloodColor "DarkGreen"
  Tag "Shadow Elemental"
  DamageFactor "Antiair", 3.0
  States
  {
  Spawn:
    SHPA A 0
	TNT1 A 0 A_JumpIf(ACS_NamedExecuteWithResult("Dehacked_MonsterReset", DEH_SCYTHEX, 10, 12), "Reset")
    Goto Idle
  Reset:
	TNT1 A 0 A_ChangeFlag("COUNTKILL", 0)
	TNT1 A 0 A_SpawnItemEx("DEHPainElemental", 0, 0, 0, 0, 0, 0, 0, SXF_TRANSFERAMBUSHFLAG|SXF_TRANSFERPOINTERS|SXF_NOCHECKPOSITION, 0)
	Stop
  Idle:
    SHPA A 10 A_Look
    Loop
  See:
    SHPA AABBCC 3 A_Chase
    Loop
  Missile:
    SHPA DE 5 A_FaceTarget
    SHPA F 5 Bright A_CustomMissile("ShadowImpBall",32,0,0)
    SHPA E 5 A_FaceTarget
    SHPA F 5 Bright A_CustomMissile("ShadowImpBall",32,0,0)
    Goto See
  Pain:
    SHPA G 6
    SHPA G 6 A_Pain
    Goto See
  Death:
    SHPA H 8 Bright
    SHPA I 8 Bright A_Scream
    SHPA JK 8 Bright
    SHPA L 8 Bright A_PainDie("ShadowSoul")
    SHPA M 8 Bright
    Stop
  Raise:
    SHPA MLKJIH 8
    Goto See
  }
}