ACTOR SubvertedGuard : Chaingunguy
{
  DamageFactor "Exorcist", 3.0
  Obituary "%o was perforated by a subverted guard."
  Tag "Subverted Guard"
  States
  {
  Spawn:
    GPOS A 0
    TNT1 A 0 A_JumpIf(ACS_NamedExecuteWithResult("Dehacked_MonsterReset", DEH_STRAIN, 1, 1), "Reset")
    Goto Idle
  Reset:
    TNT1 A 0 A_ChangeFlag("COUNTKILL", 0)
    TNT1 A 0 A_SpawnItemEx("DEHChaingunguy", 0, 0, 0, 0, 0, 0, 0, SXF_TRANSFERAMBUSHFLAG|SXF_TRANSFERPOINTERS|SXF_NOCHECKPOSITION, 0)
    Stop
  Idle:
    GPOS AB 10 A_Look
    Loop
  See:
    GPOS AABBCCDD 3 A_Chase
    Loop
  Missile:
    GPOS E 10 A_FaceTarget
    GPOS FE 4 Bright A_CPosAttack
    GPOS F 1 A_CPosRefire
    Goto Missile+1
  Pain:
    GPOS G 3
    GPOS G 3 A_Pain
    Goto See
  Death:
    GPOS H 5
    GPOS I 5 A_Scream
    GPOS J 5 A_NoBlocking
    GPOS KLM 5
    GPOS N -1
    Stop
  XDeath:
    GPOS O 5 
    GPOS P 5 A_XScream
    GPOS Q 5 A_NoBlocking
    GPOS RS 5
    GPOS T -1
    Stop
  Raise:
    GPOS N 5
    GPOS MLKJIH 5
    Goto See
  }
}

ACTOR StrainFastImp : DoomImp
{
  DamageFactor "Extinguishing", 3.0
  SeeSound "fastimp/sight"
  HitObituary "%o was shredded by a class-2 fast imp."
  Obituary "%o let a class-2 fast imp get him."
  Tag "Class-2 Fast Imp"
  States
  {
  Spawn:
    TROO A 0
    TNT1 A 0 A_JumpIf(ACS_NamedExecuteWithResult("Dehacked_MonsterReset", DEH_STRAIN, 3, 7), "Reset")
    Goto "Super::Spawn"
  Reset:
    TNT1 A 0 A_ChangeFlag("COUNTKILL", 0)
    TNT1 A 0 A_SpawnItemEx("ValiantImp", 0, 0, 0, 0, 0, 0, 0, SXF_TRANSFERAMBUSHFLAG|SXF_TRANSFERPOINTERS|SXF_NOCHECKPOSITION, 0)
    Stop
  See:
    TROO AAAABBBBCCCCDDDD 1 A_Chase
    Loop
  Melee:
  Missile:
    TNT1 A 0 A_JumpIfInventory("LDLegendaryMonsterTransformed", 1, "LegendaryMissile")
    TROO EF 8 A_FaceTarget
    TROO G 6 A_TroopAttack
    TROO G 0 A_SpidRefire
    Loop
  LegendaryMissile:
    TROO EF 5 A_FaceTarget
    TROO G 5 A_TroopAttack
    TROO G 0 A_SpidRefire
    Loop
  }
}

ACTOR Doppleganger : LostSoul //Where do they get these names from?
{
  DamageFactor "Antiair", 3.0
  Obituary "%o was spooked by a doppleganger."
  Damage 2
  Tag "Doppleganger"
  PainChance 200
  Height 48 //Don't ask me why it's shorter - it's like that in STRAIN.
  States
  {
  Spawn:
    SKU2 A 0
    TNT1 A 0 A_JumpIf(ACS_NamedExecuteWithResult("Dehacked_MonsterReset", DEH_STRAIN, 4, 7), "Reset")
    Goto Idle
  Reset:
    TNT1 A 0 A_ChangeFlag("COUNTKILL", 0)
    TNT1 A 0 A_SpawnItemEx("DEHLostSoul", 0, 0, 0, 0, 0, 0, 0, SXF_TRANSFERAMBUSHFLAG|SXF_TRANSFERPOINTERS|SXF_NOCHECKPOSITION, 0)
    Stop
  Idle:
    SKU2 AB 10 Bright A_Look
    Loop
  See:
    SKU2 AB 6 Bright A_Chase
    Loop
  Missile:
    SKU2 C 10 Bright A_FaceTarget
    SKU2 D 10 Bright A_TroopAttack
    SKU2 CDC 10 Bright A_FaceTarget
    SKU2 D 4 Bright A_SkullAttack
    SKU2 CD 4 Bright
    Goto Missile+6
  Pain:
    SKU2 E 3 Bright
    SKU2 E 3 Bright A_Pain
    Goto See
  Death:
    SKU2 F 6 Bright
    SKU2 G 6 Bright A_Scream
    SKU2 H 6 Bright
    SKU2 I 6 Bright A_NoBlocking
    SKU2 J 6
    SKU2 K 6
    Stop
  }
}

ACTOR Holobot : Cacodemon
{
  DamageFactor "Antiair", 3.0
  Health 200
  Speed 10
  PainSound ""
  ActiveSound "holobot/active"
  SeeSound "holobot/sight"
  DeathSound "holobot/death"
  MeleeSound ""
  Obituary "%o was unable to hide from a holobot."
  Tag "Holobot"
  States
  {
  Spawn:
    FBOT A 0
    TNT1 A 0 A_JumpIf(ACS_NamedExecuteWithResult("Dehacked_MonsterReset", DEH_STRAIN, 6, 5), "Reset")
    Goto Idle
  Reset:
    TNT1 A 0 A_ChangeFlag("COUNTKILL", 0)
    TNT1 A 0 A_SpawnItemEx("DEHCacodemon", 0, 0, 0, 0, 0, 0, 0, SXF_TRANSFERAMBUSHFLAG|SXF_TRANSFERPOINTERS|SXF_NOCHECKPOSITION, 0)
    Stop
  Idle:
    FBOT A 3 A_Look
    Loop
  See:
    FBOT ABCDEF 3 A_Chase
    Loop
  Melee:
    FBOT A 20 A_FaceTarget
    FBOT G 6 A_CustomMeleeAttack(random(1, 10) * 6, "holobot/melee", "holobot/melee")
    Goto See
  Missile:
    TNT1 A 0 A_JumpIfInventory("LDLegendaryMonsterTransformed", 1, "LegendaryMissile")
    FBOT AA 5 A_FaceTarget
    FBOT A 5 Bright A_CustomMissile("HolobotRevenantTracer", 32)
    Goto See
  LegendaryMissile:
    FBOT AA 5 A_FaceTarget
    FBOT A 5 Bright A_CustomMissile("RevenantTracer", 32)
    Goto See
  Pain:
    FBOT A 3
    FBOT A 3 A_Pain
    FBOT A 6
    Goto See
  Death:
    FBOT H 10 Bright
    FBOT I 10 Bright A_Scream
    FBOT J 10 Bright A_NoBlocking
    FBOT K 10 Bright
    FBOT L 10 Bright
    FBOT M 10 Bright
    Stop
  Raise:
    Stop
  }
}
ACTOR HolobotRevenantTracer : RevenantTracer
{
  Speed 4
}
ACTOR InvisibleHolobot : HoloBot
{
  DamageFactor "Antiair", 3.0
  +SHADOW
  RenderStyle "OptFuzzy"
  Alpha 0.5
  Obituary "%o was lost sight of an invisible holobot."
  Tag "Invisible Holobot"
  States
  {
  Spawn:
    FBOT A 0
    TNT1 A 0 A_JumpIf(ACS_NamedExecuteWithResult("Dehacked_MonsterReset", DEH_STRAIN, 11, 5), "Reset")
    Goto Idle
  Reset:
    TNT1 A 0 A_ChangeFlag("COUNTKILL", 0)
    TNT1 A 0 A_SpawnItemEx("DEHPainElemental", 0, 0, 0, 0, 0, 0, 0, SXF_TRANSFERAMBUSHFLAG|SXF_TRANSFERPOINTERS|SXF_NOCHECKPOSITION, 0)
    Stop
  }
}

ACTOR StrainDemon : Demon
{
  DamageFactor "Wrangler", 3.0
  Obituary "%o was bit by a skinless demon."
  Tag "Skinless Demon"
  Health 217 // I've always found it weird how it has exactly 217 health.
  States // Maybe there's something about the number I'm missing, but's strange.
  {
  Spawn:
    SAR2 A 0
    TNT1 A 0 A_JumpIf(ACS_NamedExecuteWithResult("Dehacked_MonsterReset", DEH_STRAIN, 2, 7), "Reset")
    Goto Idle
  Reset:
    TNT1 A 0 A_ChangeFlag("COUNTKILL", 0)
    TNT1 A 0 A_SpawnItemEx("DEHDemon", 0, 0, 0, 0, 0, 0, 0, SXF_TRANSFERAMBUSHFLAG|SXF_TRANSFERPOINTERS|SXF_NOCHECKPOSITION, 0)
    Stop
  Idle:
    SAR2 AB 10 A_Look
    Loop
  See:
    SAR2 AABBCCDD 2 Fast A_Chase
    Loop
  Melee:
    SAR2 EF 8 Fast A_FaceTarget
    SAR2 G 8 Fast A_SargAttack
    Goto See
  Pain:
    SAR2 H 2 Fast
    SAR2 H 2 Fast A_Pain
    Goto See
  Death:
    SAR2 I 8
    SAR2 J 8 A_Scream
    SAR2 K 4
    SAR2 L 4 A_NoBlocking
    SAR2 M 4
    SAR2 N -1
    Stop
  Raise:
    SAR2 N 5
    SAR2 MLKJI 5
    Goto See
  }
}

ACTOR MinisterOfHate
{
  DamageFactor "Scrapper", 3.0
  Health 300
  Radius 20
  Height 55
  Mass 300
  Speed 9
  PainChance 40
  Monster
  +FLOORCLIP
  +NORADIUSDMG
  SeeSound "cyberjr/sight"
  PainSound "cyber/pain"
  DeathSound "cyber/death"
  ActiveSound "cyber/active"
  Tag "Minister of Hate"
  Obituary "%o was splattered by a Minister of Hate."
  States
  {
  Spawn:
    CYBJ A 0
    TNT1 A 0 A_JumpIf(ACS_NamedExecuteWithResult("Dehacked_MonsterReset", DEH_STRAIN, 13, 12), "Reset")
    Goto Idle
  Reset:
    TNT1 A 0 A_ChangeFlag("COUNTKILL", 0)
    TNT1 A 0 A_SpawnItemEx("HellCleric", 0, 0, 0, 0, 0, 0, 0, SXF_TRANSFERAMBUSHFLAG|SXF_TRANSFERPOINTERS|SXF_NOCHECKPOSITION, 0)
    Stop
  Idle:
    CYBJ AB 10 A_Look
    Loop
  See:
    CYBJ A 3 A_PlaySound("cyberjr/hoof", CHAN_BODY, 1.0, 0, ATTN_NORM)
    CYBJ ABBCC 3 A_Chase
    CYBJ D 3 A_Metal
    CYBJ D 3 A_Chase
    Loop
  Missile:
    CYBJ E 12 A_FaceTarget
    CYBJ F 12 Bright A_CyberAttack
    Goto See
  Pain:
    CYBJ G 10 A_Pain
    Goto See
  Death:
    CYBJ H 5
    CYBJ I 5 A_Scream
    CYBJ JKL 5
    CYBJ M 5 A_NoBlocking
    CYBJ NO 5
    CYBJ P -1
    Stop
  }
}

ACTOR StrainDemonLord : BaronOfHell
{
  DamageFactor "Blessed", 3.0
  SeeSound "demonlord/sight"
  DeathSound "demonlord/death"
  Health 1400
  Obituary "%o was flattened by a Demon Lord."
  HitObituary "%o was ripped apart by a Demon Lord."
  Tag "Demon Lord"
  States //He doesn't actually have a melee attack, interestingly enough.
  {
  Spawn:
    BOS4 A 0
    TNT1 A 0 A_JumpIf(ACS_NamedExecuteWithResult("Dehacked_MonsterReset", DEH_STRAIN, 15, 21), "Reset")
    Goto Idle
  Reset:
    TNT1 A 0 A_ChangeFlag("COUNTKILL", 0)
    TNT1 A 0 A_SpawnItemEx("Belphegor", 0, 0, 0, 0, 0, 0, 0, SXF_TRANSFERAMBUSHFLAG|SXF_TRANSFERPOINTERS|SXF_NOCHECKPOSITION, 0)
    Stop
  Idle:
    BOS4 AB 10 A_Look
    Loop
  See:
    BOS4 AABBCCDD 3 A_Chase
    Loop
  Melee: //Change: fixed the animation so he attacks at the right speed.
  Missile:
    BOS4 EF 8 A_FaceTarget
    BOS4 G 1 A_FatAttack1("DemonLordBall")
    BOS4 G 1 A_FatAttack2("DemonLordBall")
    BOS4 G 6 A_FatAttack3("DemonLordBall")
    Goto See
  Pain:
    BOS4 H 2
    BOS4 H 2 A_Pain
    Goto See
  Death:
    BOS4 I 8
    BOS4 J 8 A_Scream
    BOS4 K 8
    BOS4 L 8 A_NoBlocking
    BOS4 MN 8
    BOS4 O -1 A_BossDeath
    Stop
  Raise:
    BOS4 O 8
    BOS4 NMLKJI 8
    Goto See
  }
}
ACTOR DemonLordBall : BaronBall
{
  Speed 15
  Damage 5
}